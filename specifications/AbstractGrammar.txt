CATEGORIES

expression;
definition;
statement;
type;

NODES

program -> definition*;

varDefinition:definition -> string type;
structDefinition:definition -> string fieldDefinition*;
fieldDefinition:definition -> string type;
functionDefinition:definition -> string parameters:varDefinition* type? localVariables:varDefinition* statement*;

print:statement -> expressions:expression*;
read:statement -> expression;
functionCallStatement:statement -> string expression*;
assignment:statement -> left:expression right:expression;
conditional:statement -> expression ifStatements:statement* elseStatements:statement*;
while:statement -> expression loopStatements:statement*;
return:statement -> expression?;

intType:type -> ;
floatType:type -> ;
charType:type -> ;
arrayType:type -> int type;
structType:type -> string;

variable:expression -> string;
intLiteral:expression -> int;
floatLiteral:expression -> float;
charLiteral:expression -> char;
functionCallExpression:expression -> string expression*;
structAccess:expression -> expression string;
arrayAccess:expression -> left:expression right:expression;
cast:expression -> castType:type expression;
arithmetic:expression -> left:expression operator:string right:expression;
comparison:expression -> left:expression operator:string right:expression;
logicBinary:expression -> left:expression operator:string right:expression;
logicUnary:expression -> operator:string expression;

ATTRIBUTE GRAMMAR Identification

variable -> varDefinition;
functionCallStatement -> functionDefinition;
functionCallExpression -> functionDefinition;
structType -> structDefinition;
fieldDefinition -> structDefinition;

ATTRIBUTE GRAMMAR TypeChecking

expression -> type;
expression -> lvalue:boolean;
statement -> function:functionDefinition;

ATTRIBUTE GRAMMAR MemoryAllocation

varDefinition -> [inh] address:int;
structDefinition -> [inh] address:int;
fieldDefinition -> [inh] address:int;
functionDefinition -> [inh] address:int;

// -----------------------
CODE SPECIFICATION Mapl

run[program]
execute[statement]
value[expression]
address[expression]
define[definition]

metadata[program]
metadata[definition]